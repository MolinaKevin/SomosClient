plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "dev.flutter.flutter-gradle-plugin"
}

android {
    namespace "com.somos.client.somos"
    compileSdk = 36

    defaultConfig {
        applicationId "com.somos.client.somos"
        minSdk = 26
        targetSdk = 36

        def lp = new Properties()
        def f = rootProject.file("local.properties")
        if (f.exists()) {
            f.withInputStream { lp.load(it) }
        }
        versionCode = (lp.getProperty("flutter.versionCode") ?: "1").toInteger()
        versionName = lp.getProperty("flutter.versionName") ?: "1.0.0"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions { jvmTarget = "17" }

    buildTypes {
        release {
            signingConfig = signingConfigs.debug
        }
    }
}

flutter {
    source '../..'
}

tasks.register("copyReleaseToFlutterOut", Copy) {
    dependsOn("assembleRelease")

    from("$buildDir/outputs/apk/release/app-release.apk")

    into("$projectDir/../../build/app/outputs/flutter-apk")

    rename { "app-release.apk" }
}

afterEvaluate {
    tasks.matching { it.name == "assembleRelease" }.configureEach {
        finalizedBy("copyReleaseToFlutterOut")
    }
}
